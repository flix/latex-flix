\documentclass{article}

\usepackage{inconsolata}
\usepackage{microtype}

\usepackage{flix}

\lstset{language=flix}

\begin{document}

\section{Algebraic Data Types and Pattern Matching}

\begin{lstlisting}
/// An algebraic data type for shapes.
enum Shape {
    case Circle(Int32),          // circle radius
    case Square(Int32),          // side length
    case Rectangle(Int32, Int32) // height and width
}

/// Computes the area of the given shape using 
/// pattern matching and basic arithmetic.
def area(s: Shape): Int32 = match s {
    case Circle(r)       => 3 * (r * r)
    case Square(w)       => w * w
    case Rectangle(h, w) => h * w
}

// Computes the area of a 2 by 4.
def main(): Int = area(Rectangle(2, 4))    
\end{lstlisting}

\section{Lists and List Processing}

\begin{lstlisting}
/// In Flix, as in many functional programming languages, 
/// lists are the bread and butter.

/// We can easily construct a list:
def aList(): List[Int32] = 1 :: 2 :: 3 :: Nil

/// We can easily append two lists:
def bList(): List[Int32] = aList() ::: aList()

/// We can use pattern matching to take a list apart:
def length[a](l: List[a]): Int = match l {
    case Nil     => 0
    case x :: xs => 1 + length(xs) 
}

/// The Flix library has extensive support for lists:
def main(): Bool = 
    let l1 = List.range(0, 10);
    let l2 = List.intersperse(42, l1);
    let l3 = List.map(x -> x :: x :: Nil, l2);
    let l4 = List.flatten(l3);
    List.exists(x -> x == 0, l4)
\end{lstlisting}

\section{Deriving Type Classes}
\begin{lstlisting}
/// We derive the type classes Eq, Order, and ToString
/// for the enum Month
enum Month with Eq, Order, ToString {
    case January
    case February
    case March
    case April
    case May
    case June
    case July
    case August
    case September
    case October
    case November
    case December
}

type alias Year = Int32
type alias Day = Int32

/// The Date type derives the type classes Eq and Order
enum Date(Year, Month, Day) with Eq, Order

/// We implement our own instance of ToString for Date
/// since we don't want the default "Date(1948, December, 10)"
instance ToString[Date] {
    pub def toString(x: Date): String =
        let Date.Date(y, m, d) = x;
        "${d} ${m}, ${y}"
}

/// Thanks to the Eq and Order type classes,
/// we can easily compare dates.
def earlierDate(d1: Date, d2: Date): Date = Order.min(d1, d2)

/// Thanks to the ToString type class,
/// we can easily convert dates to strings.
def printDate(d: Date): Unit \ IO =
    let message = "The date is ${d}!";
    println(message)
\end{lstlisting}

\end{document}
